buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.postgresql:postgresql:9.4.1212'
    }
}

plugins {
    id 'java'
    id 'application'
    id 'org.flywaydb.flyway' version '4.1.2'
    id 'eclipse'
}

version = '0.5'
sourceCompatibility = '1.8'
def vertxVersion = '3.4.2'

repositories {
    jcenter()
}

dependencies {
    compile "io.vertx:vertx-core:${vertxVersion}"
    compile "io.vertx:vertx-web:${vertxVersion}"
    compile "io.vertx:vertx-mysql-postgresql-client:${vertxVersion}"
    compile "io.vertx:vertx-auth-oauth2:${vertxVersion}"
    compile 'org.flywaydb:flyway-core:4.1.2'

    testCompile "junit:junit:4.12"
    testCompile "io.vertx:vertx-unit:${vertxVersion}"
    testCompile 'com.github.stefanbirkner:system-rules:1.16.0'

    // needed also by PostgreSQL
    runtime 'ch.qos.logback:logback-classic:1.2.2'
}

ext {
    configFileName = project.hasProperty('configFileName') ? project.getProperty('configFileName') : 'config.test.json'
    configFile = file configFileName
    configurationAtBuildTime = new groovy.json.JsonSlurper().parse configFile
    databaseConfig = configurationAtBuildTime.database
    // vertxLoggingSystemProperties = [
    //     'java.util.logging.config.file': 'logging.properties'
    // ]
    mainVerticleName = 'de.thokari.epages.app.EpagesAppMainVerticle'
}

mainClassName = 'io.vertx.core.Launcher'

apply from: 'gradle/docker.gradle'

// Vert.x will watch for file changes in this location
def watchForChange = "src/**/*.java,${configFile}"
// Vert.x will call this task on changes
def doOnChange = './gradlew classes'

run {
    args = [
        'run', mainVerticleName,
        "-conf", configFile.absolutePath,
        "--launcher-class=$mainClassName",
        "--redeploy=$watchForChange",
        "--on-redeploy=$doOnChange"
    ]
    // systemProperties = vertxLoggingSystemProperties
    doFirst {
        println "Using arguments: ${args}"
    }
}

flyway {
    url = "jdbc:postgresql://${databaseConfig.host}:${databaseConfig.port}/${databaseConfig.database}"
    user = databaseConfig.username
    password = databaseConfig.password
}

test {
    dependsOn flywayClean, flywayMigrate
    outputs.upToDateWhen { false }
    // systemProperties = vertxLoggingSystemProperties
}
