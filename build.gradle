plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
    id 'eclipse'
}

repositories {
    jcenter()
}

sourceCompatibility = '1.8'

def vertxVersion = '3.3.3'

dependencies {
    compile "io.vertx:vertx-core:$vertxVersion"
    compile "io.vertx:vertx-web:$vertxVersion"
    compile "io.vertx:vertx-auth-oauth2:$vertxVersion"
	testCompile "junit:junit:4.12"
	testCompile "io.vertx:vertx-unit:$vertxVersion"
}

mainClassName = 'io.vertx.core.Launcher'
def mainVerticleName = "de.thokari.epages.EpagesAppMainVerticle"

// Vert.x watches for file changes in all subdirectories
// of src/ but only for files with .java extension
def watchForChange = 'src/**/*.java'
// Vert.x will call this task on changes
def doOnChange = './gradlew classes'
run {
    args = ['run', mainVerticleName, "--redeploy=$watchForChange", "--launcher-class=$mainClassName", "--on-redeploy=$doOnChange", "-conf", "config.test.json"]
}

shadowJar {
    classifier = 'fat'
    manifest {
        attributes "Main-Verticle": mainVerticleName
    }
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}
