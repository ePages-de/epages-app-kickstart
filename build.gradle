plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
    id 'eclipse'
}

repositories {
    jcenter()
}

sourceCompatibility = '1.8'

def vertxVersion = '3.3.3'

dependencies {
    compile "io.vertx:vertx-core:$vertxVersion"
    compile "io.vertx:vertx-web:$vertxVersion"
    compile "io.vertx:vertx-auth-oauth2:$vertxVersion"
    testCompile "junit:junit:4.12"
    testCompile "io.vertx:vertx-unit:$vertxVersion"
}

def configFileName = 'config.test.json' 
def mainVerticleName = 'de.thokari.epages.EpagesAppMainVerticle'
mainClassName = 'io.vertx.core.Launcher'

// Vert.x will watch for file changes in this location
def watchForChange = "src/**/*.java,${configFileName}"
// Vert.x will call this task on changes
def doOnChange = './gradlew classes'

run {
    args = [
        'run', mainVerticleName,
        "-conf", configFileName,
        "--launcher-class=$mainClassName",
        "--redeploy=$watchForChange",
        "--on-redeploy=$doOnChange"
    ]
}

shadowJar {
    classifier = 'fat'
    manifest {
        attributes "Main-Verticle": mainVerticleName
    }
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}
